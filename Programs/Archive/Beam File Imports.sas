/*---------------------------------------------------------------------

	AUTHOR: JUSTIN HUBBARD - EMERGENT BUSINESS GROUP
	
	PURPOSE: IMPORT BEAM FILES (PIPE DELIMITED)
	
	STEPS:

		1) ASSIGNS LIBRARIES AND LOCATIONS
		2) SCANS LIBRARIES FOR ALL FILE TYPES TO IDENTIFY FILES THAT NEED TO BE IMPORTED
		3) LOOPS THROUGH FILES THAT NEED TO BE IMPORTED
		4) COMBINES BEAM FILES AND OUTPUTS TO OUTPUT LIBRARY

-----------------------------------------------------------------------*/
OPTIONS MPRINT MLOGIC SYMBOLGEN VALIDVARNAME=ANY;

FILENAME GEO_IN PIPE 'DIR "E:\SHARED\CADA\SAS SOURCE CODE\DEVELOPMENT\JHUBBARD\EBG\1_PORTFOLIO_FILE_STANDARDIZATION\PRODUCTION ENVIRONMENT TEST\IMPORT FROM BEAM\*.*" /B ';

%LET FILE_LOC_IN = "E:\SHARED\CADA\SAS SOURCE CODE\DEVELOPMENT\JHUBBARD\EBG\1_PORTFOLIO_FILE_STANDARDIZATION\PRODUCTION ENVIRONMENT TEST\IMPORT FROM BEAM";

%LET DATE=%SYSFUNC(INTNX(DAY,%SYSFUNC(TODAY()),0,END),DATE7.);
%PUT &DATE;

DATA DIRLIST_GEO_IN;
       INFILE GEO_IN LRECL=200 TRUNCOVER;
       INPUT FILE_NAME $150.;
	   LENGTH = LENGTH(FILE_NAME);
	   FILE_NAME_FULL = &FILE_LOC_IN.||"\"||FILE_NAME;
	   EXTENSION = SCAN(TRANWRD(COMPRESS(FILE_NAME_FULL),'.',' '),2);
RUN;

PROC SQL;
	SELECT COUNT(FILE_NAME) INTO: COUNT_BEAM_IN
	FROM DIRLIST_GEO_IN
;
QUIT;

PROC SORT DATA=DIRLIST_GEO_IN NODUPKEY;
BY FILE_NAME_FULL ;
RUN;

DATA COUNT_FILES_IN;
	SET DIRLIST_GEO_IN;
	BY FILE_NAME_FULL;
	RETAIN COUNT 0;
	COUNT+1;
	TOTAL_COUNTER = COUNT;
RUN;


%MACRO CHECK_FIRST_FILE_IN(FILE=,TOTAL_COUNTER=,FILE_NAME=,EXTENSION=);

	%IF &TOTAL_COUNTER.= 1  %THEN 
		%DO;

			/*FILENAME IN_F "&FILE";*/
			
			%PUT &FILE;
			%PUT &EXTENSION;

			DATA _NULL_;
				CALL SYMPUTX("FILE_USE","&FILE",'G');
				CALL SYMPUTX("TOTAL_COUNTER_USE",&TOTAL_COUNTER.,'G');
			RUN;
			
			/*IMPORT PIPE DELIMITED FILE	*/
			%INCLUDE "&WHEREPRGMSAT.\BEAM FILE IMPORT.SAS";
				%BEAM_IMPORT
			/*BEAM_ACOUNT_EXTRACT IS CREATED*/

		%END;

	%ELSE %IF &TOTAL_COUNTER. ^= 1 %THEN 
		%DO;

			%PUT &FILE;
			%PUT &EXTENSION;

			DATA _NULL_;
				CALL SYMPUTX("FILE_USE","&FILE",'G');
				CALL SYMPUTX("TOTAL_COUNTER_USE",&TOTAL_COUNTER.,'G');
			RUN;	

			/*IMPORT PIPE DELIMITED FILE	*/
			%INCLUDE "&WHEREPRGMSAT.\BEAM FILE IMPORT.SAS";
				%BEAM_IMPORT
			/*BEAM_ACOUNT_EXTRACT_&TOTAL_COUNTER. IS CREATED*/


			PROC APPEND BASE=ACOUNT_EXTRACT DATA=BEAM_ACOUNT_EXTRACT_&TOTAL_COUNTER. FORCE;
			RUN;

		%END;
%MEND;

PROC SQL;
	DROP TABLE ACOUNT_EXTRACT;
QUIT;


DATA _NULL_;
	SET COUNT_FILES_IN;
	CALL EXECUTE('%NRSTR(%CHECK_FIRST_FILE_IN(FILE='||FILE_NAME_FULL||',TOTAL_COUNTER='||PUT(COUNT,8.)||',FILE_NAME='||FILE_NAME||',EXTENSION='||EXTENSION||'))');
RUN;






